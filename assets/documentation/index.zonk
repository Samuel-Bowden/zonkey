class Section {
	[Hyperlink] subsections;
	Hyperlink intro;

	constructor() {
		@subsections = Hyperlink[];
	}

	method add_intro(String name, String location) -> Section {
		@intro = Hyperlink(name, "zonkey:documentation/" + location);
		return self;
	}

	method add_subsection(String name, String location) -> Section {
		@subsections.push(Hyperlink(name, "zonkey:documentation/" + location));
		return self;
	}

	method get_intro() -> Hyperlink {
		return @intro;
	}

	method get_subsections() -> [Hyperlink] {
		return @subsections;
	}
}

class ZonkeyDocumentation {
	Page page;
	[Section] sections;

	constructor() {
		let prefix = "zonkey:documentation/";

		@sections = Section[
			Section()
				.add_intro("Getting started", "learning_language.zonk")
				.add_subsection("Creating And Running Your First Script", "creating_running_first_script.zonk")
				.add_subsection("Variables, Types And Expressions", "variables_types_and_expressions.zonk")
				.add_subsection("Functions", "functions.zonk")
				.add_subsection("Classes", "classes.zonk")
				.add_subsection("Overview of GUI classes", "overview_of_gui_classes.zonk")
				.add_subsection("Creating your first page", "creating_your_first_page.zonk")
				.add_subsection("Loops and Arrays", "loops_and_arrays.zonk")
				.add_subsection("A More Advanced Page", "a_more_advanced_page.zonk"),
			Section()
				.add_intro("Standard Prelude Reference", "standard_prelude.zonk"),
			Section()
				.add_intro("Grammar", "grammar.zonk")
		];

		@page = Page()
			.set_title("Documentation")
			.center()
			.set_max_width(800.)
			.set_background_colour("#FDF6E3")
			.set_text_colour("#5C6A72")
			.add(Text("Zonkey Documentation").set_size(70.0));

		for (let i = 0, i < @sections.len(), i += 1) {
			let section = @sections.get(i);
			@page.add(Row().add(Text(integer_to_string(i + 1))).add(section.get_intro()));

			let subsections = section.get_subsections();

			for (let j = 0, j < subsections.len(), j += 1) {
				@page.add(
					Row()
						.add(Text(
							"    " + 
							integer_to_string(i + 1) +
							"." + 
							integer_to_string(j + 1)
						))
						.add(subsections.get(j))
				);
			}
		}

		set_page(@page);
	}
}

start {
	ZonkeyDocumentation();
}
