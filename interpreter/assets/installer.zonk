start {
    if (args().len() < 4) {
        let message = "Incorrect arguments. Requires: <APPLICATION_ID> <APPLICATION_NAME> <WINDOWS_SHORTCUT_LOCATION> <LINUX_SHORTCUT_LOCATION> <REQUIRED_FILES>...";
        set_page(Page().add(Text(message)));
        println(message);
        return;
    }

    let application_id = args().get(0);
    let application_name = args().get(1);
    let windows_shortcut_location = args().get(2);
    let linux_shortcut_location = args().get(3);

    let required_files = String[];

    for (let i = 4, i < args().len(), i += 1)
        required_files.push(args().get(i));

    let page = Page()
        .center()
        .set_title("Installer for " + application_name)
        .set_max_width(800.);

    page.add(Text("Installation for " + application_name).set_size(70.));
    page.add(Text("Application ID: " + application_id).set_colour("#AAAAAA"));

    page.add(Text("Are you sure you want to install " + application_name + " to your system? Please make sure you trust the author of this application.").set_colour("#AA0000"));

    page.add(Text("These files will be downloaded to your system. "));

    for (let i = 0, i < required_files.len(), i += 1)
        page.add(Text(integer_to_string(i + 1) + ". " + required_files.get(i)));

    page.add(Text("They will be placed inside a dedicated folder for this application in the following directories below depending on your operating system:"));

    page.add(Text("Linux: /home/<YOUR_USER>/.local/share/zonkey/" + application_id + "/"));

    page.add(Text("A shortcut will be added to your desktop folder if desired, which will allow you to launch this application."));

    let install_button_without_shortcut = Button("Install without shortcut").set_width_fill();
    let install_button_with_shortcut = Button("Install with shortcut").set_width_fill();

    let column = Column()
        .add(install_button_with_shortcut)
        .add(install_button_without_shortcut)
        .set_max_width(250.);

    page.add(column);

    let shortcut_desired = false;
    let install_requested = false;

    set_page(page);

    while (wait_for_event()) {
        if (install_button_with_shortcut.clicked()) {
            shortcut_desired = true;
            install_requested = true;
            break;
        }
        else if (install_button_without_shortcut.clicked()) {
            install_requested = true;
            break;
        }
    }

    if (install_requested) {
        let install_arguments = String[
            application_id,
            windows_shortcut_location,
            linux_shortcut_location
        ];

        # Add the required files to install to the end of install arguments
        for (let i = 0, i < required_files.len(), i += 1) {
            install_arguments.push(required_files.get(i));
        }
        
        install_application(
            install_arguments,
            shortcut_desired
        );

        let title = "Installation for " + application_name + " Successsful";

        let install_success_page = Page()
            .add(Text(title).set_size(70.))
            .add(Text("You can navigate away from this page now."))
            .center()
            .set_title(title)
            .set_max_width(800.);

        set_page(install_success_page);
    }
}
