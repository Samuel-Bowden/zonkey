start {
	let initial_msg = "No password entered yet. You have 4 attempts";
	let password = Input("Guess the password");
	let status = Text(initial_msg).set_colour("#f8f8f2");

	let title = "Password Guessing Game";

	let install_button = Button("Install game").set_background_colour("#6272a4").set_width_fill();
	let button_column = Column().set_max_width(150.0);
	let main_column = Column().add(status).add(password);

	let page = Page()	
		.set_title(title)
		.set_background_colour("#282a36")
		.center()
		.set_max_width(600.)
		.add(Row().add(Text(title).set_size(40.).set_colour("#f8f8f2")).center())
		.add(main_column)
		.add(button_column);

    if (!(args().len() > 0 & args().get(0) == "installed"))
        button_column.add(install_button);

    set_page(page);

	let attempts = 4;
	let start_again = Button("Start again").set_background_colour("#6272a4").set_width_fill();

	while (wait_for_event()) {
		if (start_again.clicked()) {
			main_column.add(password);
			button_column.remove(start_again);
			attempts = 4;
			status.set_text(initial_msg).set_colour("#fff");
		}

		if (install_button.clicked()) {
			let arguments = String[
				"Guessing Game",
				"zonkey:guessing_game/Guessing Game.lnk",
				"zonkey:guessing_game/Guessing Game.desktop",
				"zonkey:guessing_game/app.zonk"
			];
			open_link("zonkey:installer.zonk", arguments);
		}

		if (password.confirmed())
			if (password.get_text() == "password1234") {
				main_column.remove(password);
				button_column.add(start_again);
				status	
					.set_text("You guessed the correct password!")
					.set_colour("#50fa7b");
			} else {
				attempts -= 1;
				status
					.set_text("That wasn't the right password. Attempts left: " + integer_to_string(attempts))
					.set_colour("#ff5555");
				if (attempts == 0) {
					main_column.remove(password);
					button_column.add(start_again);
					status.set_text("Game Over");
				}
			}

		password.set_text("");
	}
}