# Zonkey Calculator

class Calculator {
	Button calc_screen;
	String screen_state;
	String current_operation;
	Integer current_number;
	Float calculation;

	Button btn_zero;
	Button btn_one;
	Button btn_two;
	Button btn_three;
	Button btn_four;
	Button btn_five;
	Button btn_six;
	Button btn_seven;
	Button btn_eight;
	Button btn_nine;

	Button btn_divide;
	Button btn_multiply;
	Button btn_subtract;
	Button btn_add;

	Button btn_reset;
	Button btn_calculate;

	method create_button(String text) -> Button {
		return Button(text)
			.set_padding(40., 30.)
			.set_width_fill();
	}

	method number_button(String text) -> Button {
		return self.create_button(text)
			.set_background_colour(0.2, 0.2, 0.2);
	}

	method operation_button(String text) -> Button {
		return self.create_button(text)
			.set_background_colour(0.4, 0.4, 0.4);
	}

	method calculator_column(
		Button first,
		Button second,
		Button third,
		Button fourth
	) -> Column {
		return Column()
			.add(fourth)
			.add(third)
			.add(second)
			.add(first)
			.set_max_width(100.);
	}

	constructor() {
		@btn_reset = self.create_button("AC").set_background_colour(0.7, 0., 0.);
		@btn_one = self.number_button(String 1);
		@btn_four = self.number_button(String 4);
		@btn_seven = self.number_button(String 7);
		let first_column = self.calculator_column(@btn_reset, @btn_one, @btn_four, @btn_seven);

		@btn_zero = self.number_button(String 0);
		@btn_two = self.number_button(String 2);
		@btn_five = self.number_button(String 5);
		@btn_eight = self.number_button(String 8);
		let second_column = self.calculator_column(@btn_zero, @btn_two, @btn_five, @btn_eight);

		@btn_calculate = self.number_button("=");
		@btn_three = self.number_button(String 3);
		@btn_six = self.number_button(String 6);
		@btn_nine = self.number_button(String 9);
		let third_column = self.calculator_column(@btn_calculate, @btn_three, @btn_six, @btn_nine);

		@btn_add = self.operation_button("+");
		@btn_subtract = self.operation_button("-");
		@btn_multiply = self.operation_button("ร");
		@btn_divide = self.operation_button("รท");
		let fourth_column = self.calculator_column(@btn_add, @btn_subtract, @btn_multiply, @btn_divide);

		@calc_screen
			.set_width_fill()
			.set_text(" ")
			.set_background_colour(0.4, 0.45, 0.4)
			.set_padding(30., 20.);

		let row = Row()
			.add(first_column)
			.add(second_column)
			.add(third_column)
			.add(fourth_column);

		Page()
			.set_title("Calculator")
			.set_background_colour(0., 0., 0.)
			.add(
				Row()
					.center()
					.add(
						Column()
							.add(@calc_screen)
							.add(row)
							.set_max_width(400.)));
	}

	method default_screen() {
		@screen_state = " ";
	}

	method change_operation(String operation) {
		if (@current_operation == "+") {
			@calculation += Float @current_number;
		} else if (@current_operation == "-") {
			@calculation -= Float @current_number;
		} else if (@current_operation == "*") {
			@calculation *= Float @current_number;
		} else if (@current_operation == "/") {
			@calculation /= Float @current_number;
		}

		@current_operation = operation;
		@current_number = 0;
	}

	method run() {
		self.default_screen();

		@current_operation = "+";

		while (wait_for_event()) {
			if (@btn_zero.clicked()) {
				@screen_state += "0";
				@current_number *= 10;
			} else if (@btn_one.clicked()) {
				@screen_state += "1";
				@current_number = (@current_number * 10) + 1;
			} else if (@btn_two.clicked()) {
				@screen_state += "2";
				@current_number = (@current_number * 10) + 2;
			} else if (@btn_three.clicked()) {
				@screen_state += "3";
				@current_number = (@current_number * 10) + 3;
			} else if (@btn_four.clicked()) {
				@screen_state += "4";
				@current_number = (@current_number * 10) + 4;
			} else if (@btn_five.clicked()) {
				@screen_state += "5";
				@current_number = (@current_number * 10) + 5;
			} else if (@btn_six.clicked()) {
				@screen_state += "6";
				@current_number = (@current_number * 10) + 6;
			} else if (@btn_seven.clicked()) {
				@screen_state += "7";
				@current_number = (@current_number * 10) + 7;
			} else if (@btn_eight.clicked()) {
				@screen_state += "8";
				@current_number = (@current_number * 10) + 8;
			} else if (@btn_nine.clicked()) {
				@screen_state += "9";
				@current_number = (@current_number * 10) + 9;
			} else if (@btn_divide.clicked()) {
				self.default_screen();
				self.change_operation("/");
			} else if (@btn_multiply.clicked()) {
				self.default_screen();
				self.change_operation("*");
			} else if (@btn_add.clicked()) {
				self.default_screen();
				self.change_operation("+");
			} else if (@btn_subtract.clicked()) {
				self.default_screen();
				self.change_operation("-");
			} else if (@btn_calculate.clicked()) {
				self.change_operation("=");
				@screen_state = String @calculation;
			} else if (@btn_reset.clicked()) {
				@current_operation = "+";
				@current_number = 0;
				@calculation = 0.;
				@screen_state = " ";
			}

			@calc_screen.set_text(@screen_state);
		}
	}
}

start {
	Calculator().run();
}
